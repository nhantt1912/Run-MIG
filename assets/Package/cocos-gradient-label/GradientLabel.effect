CCEffect %{
  techniques:
  - passes:
    - vert: sprite-vs:vert
      frag: sprite-fs:frag
      depthStencilState:
        depthTest: false
        depthWrite: false
      blendState:
        targets:
        - blend: true
          blendSrc: src_alpha
          blendDst: one_minus_src_alpha
          blendDstAlpha: one_minus_src_alpha
      rasterizerState:
        cullMode: none
      properties:
        alphaThreshold: { value: 0.5 }
        gradientColor1: { value: [1.0, 0.0, 0.0, 1.0], editor: { type: color } }
        gradientColor2: { value: [0.0, 0.0, 1.0, 1.0], editor: { type: color } }
        gradientAngle: { value: 0.0 }
        gradientMixRatio: { value: 0.5, editor: { range: [0.0, 1.0], slide: true } }
}%

CCProgram sprite-vs %{
  precision highp float;
  #include <cc-global>
  #if USE_LOCAL
    #include <cc-local>
  #endif
  #if SAMPLE_FROM_RT
    #include <common>
  #endif
  in vec3 a_position;
  in vec2 a_texCoord;
  in vec4 a_color;

  out vec4 color;
  out vec2 uv0;

  vec4 vert () {
    vec4 pos = vec4(a_position, 1);

    #if USE_LOCAL
      pos = cc_matWorld * pos;
    #endif

    #if USE_PIXEL_ALIGNMENT
      pos = cc_matView * pos;
      pos.xyz = floor(pos.xyz);
      pos = cc_matProj * pos;
    #else
      pos = cc_matViewProj * pos;
    #endif

    uv0 = a_texCoord;
    #if SAMPLE_FROM_RT
      CC_HANDLE_RT_SAMPLE_FLIP(uv0);
    #endif
    color = a_color;

    return pos;
  }
}%

// NOTE: Please check the USE_TEXTURE box
// Component maybe got the issue, so you can modify the colors directly in material
// Also the material can be applied on sprite

CCProgram sprite-fs %{
  precision highp float;
  #include <embedded-alpha>
  #include <alpha-test>

  in vec4 color;

  #if USE_TEXTURE
    in vec2 uv0;
    #pragma builtin(local)
    layout(set = 2, binding = 10) uniform sampler2D cc_spriteTexture;
  #endif

  uniform Constant {
    vec4 gradientColor1;
    vec4 gradientColor2;
    float gradientAngle;
    float gradientMixRatio;
  };

  vec4 frag () {
    vec4 o = vec4(1, 1, 1, 1);

    #if USE_TEXTURE
      vec4 texColor = CCSampleWithAlphaSeparated(cc_spriteTexture, uv0);

      #if IS_GRAY
        float gray  = 0.2126 * texColor.r + 0.7152 * texColor.g + 0.0722 * texColor.b;
        texColor.r = texColor.g = texColor.b = gray;
      #endif

      // The gradient angle
      float angleRad = radians(gradientAngle);
      float cosAngle = cos(angleRad);
      float sinAngle = sin(angleRad);

      // Rotate UV coordinates
      vec2 centeredUV = uv0 - vec2(0.5, 0.5);
      float gradientValue = (centeredUV.x * cosAngle - centeredUV.y * sinAngle) + 0.5;

      // Clamp gradient value
      gradientValue = clamp(gradientValue, 0.0, 1.0);

      // Adjust gradient based on mix ratio
      float adjustedGradient = clamp(gradientValue + (gradientMixRatio - 0.5) * 2.0, 0.0, 1.0);

      // Mix colors based on adjusted gradient
      vec3 gradientColor = mix(gradientColor1.rgb, gradientColor2.rgb, adjustedGradient);

      // Apply gradient color to the text
      o.rgb = gradientColor;
      o.a = texColor.a;
    #endif

    o *= color;
    ALPHA_TEST(o);
    return o;
  }
}%
